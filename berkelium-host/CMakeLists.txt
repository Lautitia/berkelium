CMAKE_MINIMUM_REQUIRED(VERSION 2.6)

PROJECT(berkelium-host)

IF(NOT DEFINED BERKELIUM_BUILD)
	MESSAGE(FATAL_ERROR "this project should be build using berkelium build system!")
ENDIF()

MACRO(COPY NAME FROM TO)
	ADD_CUSTOM_COMMAND(
		OUTPUT "${TO}/${NAME}"
		DEPENDS "${FROM}/${NAME}"
		COMMAND ${CMAKE_COMMAND} -E copy "${FROM}/${NAME}" "${TO}/${NAME}"
		COMMENT "Copying ${NAME}"
	)
	LIST(APPEND BERKELIUM_DIST "${TO}/${NAME}")
ENDMACRO()

MACRO(COPY_DIR FROM TO)
	FILE(GLOB DIR "${FROM}/*")
	FOREACH(FILE ${DIR})
		GET_FILENAME_COMPONENT(FN "${FILE}" NAME)
		COPY("${FN}" "${FROM}" "${TO}")
	ENDFOREACH()
ENDMACRO()

SET(ZIP_DEST "${CMAKE_CURRENT_BINARY_DIR}/zip-${BERKELIUM_PLATFORM}")

COPY("berkelium" "${CMAKE_CURRENT_SOURCE_DIR}/dist/linux" "${ZIP_DEST}")
COPY("berkelium" "${CMAKE_CURRENT_SOURCE_DIR}/run" "${ZIP_DEST}/run")
COPY("chrome.pak" "${BERKELIUM_CHROMIUM_DIR}" "${ZIP_DEST}/run")
COPY("chrome_100_percent.pak" "${BERKELIUM_CHROMIUM_DIR}" "${ZIP_DEST}/run")
COPY("resources.pak" "${BERKELIUM_CHROMIUM_DIR}" "${ZIP_DEST}/run")
COPY_DIR("${BERKELIUM_CHROMIUM_DIR}/locales" "${ZIP_DEST}/run/locales")
COPY_DIR("${BERKELIUM_CHROMIUM_DIR}/lib.target" "${ZIP_DEST}/run/lib.target")

FIND_PACKAGE(Java)

ADD_CUSTOM_COMMAND(
	OUTPUT "${BERKELIUM_DIST_ZIP}"
	DEPENDS ${BERKELIUM_DIST}
	COMMAND "${Java_JAR_EXECUTABLE}" "cfM" "${BERKELIUM_DIST_ZIP}" "-C" "${ZIP_DEST}" .
	COMMENT "Packing berkelium-${BERKELIUM_PLATFORM}.zip"
)

ADD_CUSTOM_TARGET(berkelium-dist DEPENDS "${BERKELIUM_DIST_ZIP}")
